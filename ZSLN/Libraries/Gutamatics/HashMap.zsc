class SLN_GMHasher abstract {
	abstract virtualscope uint hash(Object o);
	abstract virtualscope bool eq(Object o0, Object o1);
	static void hashCombine(out uint seed, uint other) {
		seed ^= other + 0x9E3779B9 + (seed << 6) + (seed >> 2);
	}
}

class SLN_GMBoxedInt {
	int val;
	static SLN_GMBoxedInt create(int val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	SLN_GMBoxedInt clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}
class SLN_GMIntHasher : SLN_GMHasher {
	override uint hash(Object o) { return SLN_GMBoxedInt(o).val; }
	override bool eq(Object o0, Object o1) { return SLN_GMBoxedInt(o0).val == SLN_GMBoxedInt(o1).val; }
}

class SLN_GMBoxedUInt {
	uint val;
	static SLN_GMBoxedUInt create(uint val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	SLN_GMBoxedUInt clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}
class SLN_GMUIntHasher : SLN_GMHasher {
	override uint hash(Object o) { return SLN_GMBoxedUInt(o).val; }
	override bool eq(Object o0, Object o1) { return SLN_GMBoxedUInt(o0).val == SLN_GMBoxedUInt(o1).val; }
}

class SLN_GMBoxedDouble {
	double val;
	static SLN_GMBoxedDouble create(double val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	SLN_GMBoxedDouble clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}

class SLN_GMBoxedString {
	String val;
	static SLN_GMBoxedString create(String val) {
		let ret = new();
		ret.val = val;
		return ret;
	}
	SLN_GMBoxedString clone() {
		let ret = new();
		ret.val = self.val;
		return ret;
	}
}

class SLN_GMHashSentinel {}

class SLN_GMHashUnoccupied {}

class SLN_GMKVStorage {
	Array<Object> keys;
	Array<Object> values;
	uint loadCount;

	SLN_GMHashUnoccupied unoccupied;

	SLN_GMHashSentinel empty;
	SLN_GMHashSentinel tombstone;

	const SEED32 = 0x27220a95u;
	private static uint hash(Object key, SLN_GMHasher hasher) {
		let h = hasher.hash(key);
		return h * SEED32;
	}

	Object get(Object key, SLN_GMHasher hasher) {
		let hash = hash(key, hasher);
		let size = uint(self.keys.size());
		let index = hash % size;
		let i = index;
		while (true) {
			let kb = self.keys[i];
			if (kb == self.empty) {
				break;
			}
			if (kb != self.tombstone && hasher.eq(kb, key)) {
				return self.values[i];
			}
			i = (i + 1) % size;
		}
		return self.unoccupied;
	}

	SLN_GMKVStorage resized(uint newSize, SLN_GMHasher hasher) {
		let newKV = new("SLN_GMKVStorage");
		newKV.keys.resize(newSize);
		newKV.values.resize(newSize);

		newKV.unoccupied = self.unoccupied;
		newKV.empty = self.empty;
		newKV.tombstone = self.tombstone;

		newKV.clear();

		for (let i = 0; i < self.keys.size(); i++) {
			let k = self.keys[i];
			if (k != self.empty && k != self.tombstone) {
				newKV.insert(k, self.values[i], hasher);
			}
		}

		return newKV;
	}

	void insert(Object key, Object value, SLN_GMHasher hasher) {
		let hash = hash(key, hasher);
		let size = uint(self.keys.size());
		let index = hash % size;
		{
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty) {
					break;
				}
				if (kb != self.tombstone && hasher.eq(kb, key)) {
					self.values[i] = value;
					return;
				}
				i = (i + 1) % size;
			}
		}
		{
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty || kb == self.tombstone) {
					self.keys[i] = key;
					self.values[i] = value;
					if (kb == self.empty) {
						self.loadCount += 1;
					}
					break;
				}
				i = (i + 1) % size;
			}
		}
	}

	Object remove(Object key, SLN_GMHasher hasher) {
		let hash = hash(key, hasher);
		let size = uint(self.keys.size());
		{
			let index = hash % size;
			let i = index;
			while (true) {
				let kb = self.keys[i];
				if (kb == self.empty) {
					break;
				}
				if (kb != self.tombstone && hasher.eq(kb, key)) {
					let val = self.values[i];
					self.keys[i] = self.tombstone;
					self.values[i] = NULL;
					return val;
				}
				i = (i + 1) % size;
			}
		}
		return self.unoccupied;
	}

	bool isEmpty() {
		for (let i = 0; i < self.keys.size(); i++) {
			let k = self.keys[i];
			if (k != self.empty && k != self.tombstone) {
				return false;
			}
		}
		return true;
	}

	void clear() {
		for (let i = 0; i < self.keys.size(); i++) {
			self.keys[i] = self.empty;
			self.values[i] = NULL;
		}
		self.loadCount = 0u;
	}
}

class SLN_GMHashMapIterator {
	private SLN_GMKVStorage m;
	private int i;

	static SLN_GMHashMapIterator internalCreate(SLN_GMKVStorage kv) {
		let ret = new();
		ret.m = kv;
		ret.i = 0;
		return ret;
	}

	Object, Object next() {
		while (self.i < m.keys.size()) {
			let k = m.keys[self.i];
			let v = m.values[self.i];
			self.i += 1;
			if (k != self.m.empty && k != self.m.tombstone) {
				return k, v;
			}
		}
		return self.m.unoccupied, NULL;
	}
}

class SLN_GMHashMap {
	private SLN_GMKVStorage kv;
	private SLN_GMHasher hasher;

	static SLN_GMHashMap create(SLN_GMHasher hasher) {
		let ret = new();
		ret.kv = new("SLN_GMKVStorage");
		ret.kv.keys.resize(8);
		ret.kv.values.resize(8);
		ret.kv.unoccupied = new("SLN_GMHashUnoccupied");
		ret.kv.empty = new("SLN_GMHashSentinel");
		ret.kv.tombstone = new("SLN_GMHashSentinel");
		ret.kv.clear();
		ret.hasher = hasher;
		return ret;
	}

	Object get(Object key) {
		return self.kv.get(key, self.hasher);
	}

	bool containsKey(Object key) {
		return !(self.kv.get(key, self.hasher) is "SLN_GMHashUnoccupied");
	}

	void resize(uint newSize) {
		self.kv = self.kv.resized(newSize, hasher);
	}

	void insert(Object key, Object value) {
		if (self.kv.loadCount + 1 > uint(self.kv.keys.size() * 0.5)) {
			self.resize(self.kv.keys.size() * 2);
		}
		self.kv.insert(key, value, self.hasher);
	}

	Object remove(Object key) {
		return self.kv.remove(key, self.hasher);
	}

	bool isEmpty() {
		return self.kv.isEmpty();
	}
	
	void clear() {
		self.kv.clear();
	}

	SLN_GMHashMapIterator iter() {
		return SLN_GMHashMapIterator.internalCreate(self.kv);
	}

	String debugRepr() {
		let ret = "";
		for (let i = 0; i < self.kv.keys.size(); i++) {
			ret.appendFormat("%d : ", i);
			let k = self.kv.keys[i];
			if (k == self.kv.empty) {
				ret.appendFormat("EMPTY");
			} else if (k == self.kv.tombstone) {
				ret.appendFormat("TOMBSTONE");
			} else {
				ret.appendFormat("%p -> %p", k, self.kv.values[i]);
			}
			if (i != self.kv.keys.size() - 1) {
				ret.appendFormat("\n");
			}
		}
		return ret;
	}

	double loadFactor() {
		return double(self.kv.loadCount) / double(self.kv.values.size());
	}
	uint bucketCount() {
		return self.kv.values.size();
	}
}

class SLN_GMHashSetIterator {
	private SLN_GMHashMapIterator i;

	static SLN_GMHashSetIterator internalCreate(SLN_GMHashMapIterator i) {
		let ret = new();
		ret.i = i;
		return ret;
	}

	Object next() {
		return self.i.next();
	}
}

class SLN_GMHashSet {
	private SLN_GMHashMap hm;

	static SLN_GMHashSet create(SLN_GMHasher hasher) {
		let ret = new();
		ret.hm = SLN_GMHashMap.create(hasher);
		return ret;
	}

	bool contains(Object key) {
		return self.hm.containsKey(key);
	}

	void resize(uint newSize) {
		self.hm.resize(newSize);
	}

	void insert(Object key) {
		self.hm.insert(key, NULL);
	}

	Object remove(Object key) {
		return self.hm.remove(key);
	}

	bool isEmpty() {
		return self.hm.isEmpty();
	}

	void clear() {
		self.hm.clear();
	}

	SLN_GMHashSetIterator iter() {
		return SLN_GMHashSetIterator.internalCreate(self.hm.iter());
	}

	String debugRepr() {
		return self.hm.debugRepr();
	}
}
