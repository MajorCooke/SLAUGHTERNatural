class SLN_JsonElementOrError {}

class SLN_JsonElement : SLN_JsonElementOrError abstract
{
	abstract string Serialize();
	abstract string GetPrettyName();
}

class SLN_JsonNumber : SLN_JsonElement abstract
{
	abstract SLN_JsonNumber Negate();
	abstract double  asDouble();
	abstract int asInt();
	
	override string GetPrettyName()
	{
		return "Number";
	}
}

class SLN_JsonInt : SLN_JsonNumber
{
	int i;
	
	static SLN_JsonInt make(int i = 0)
	{
		let elem = new("SLN_JsonInt");
		elem.i = i;
		return elem;
	}
	override SLN_JsonNumber Negate()
	{
		i = -i;
		return self;
	}
	override string Serialize()
	{
		return ""..i;
	}
	
	override double asDouble()
	{
		return double(i);
	}
	
	override int asInt()
	{
		return i;
	}
}

class SLN_JsonDouble : SLN_JsonNumber
{
	double d;
	
	static SLN_JsonDouble Make(double d = 0)
	{
		SLN_JsonDouble elem = new("SLN_JsonDouble");
		elem.d = d;
		return elem;
	}
	override SLN_JsonNumber Negate()
	{
		d = -d;
		return self;
	}
	override string Serialize()
	{
		return ""..d;
	}
	
	override double asDouble()
	{
		return d;
	}
	
	override int asInt()
	{
		return int(d);
	}
}

class SLN_JsonBool : SLN_JsonElement
{
	bool b;
	
	static SLN_JsonBool Make(bool b = false)
	{
		SLN_JsonBool elem = new("SLN_JsonBool");
		elem.b = b;
		return elem;
	}
	
	override string Serialize()
	{
		return b? "true" : "false";
	}
	
	override string GetPrettyName()
	{
		return "Bool";
	}
}

class SLN_JsonString : SLN_JsonElement
{
	string s;
	
	static SLN_JsonString make(string s = "")
	{
		SLN_JsonString elem = new("SLN_JsonString");
		elem.s=s;
		return elem;
	}
	
	override string Serialize()
	{
		return SLN_JSON.serialize_string(s);
	}
	
	override string GetPrettyName()
	{
		return "String";
	}
}

class SLN_JsonNull : SLN_JsonElement
{
	static SLN_JsonNull Make()
	{
		return new("SLN_JsonNull");
	}
	
	override string Serialize()
	{
		return "null";
	}
	
	override string GetPrettyName()
	{
		return "Null";
	}
}

class SLN_JsonError : SLN_JsonElementOrError
{
	String what;
	
	static SLN_JsonError make(string s)
	{
		SLN_JsonError err = new("SLN_JsonError");
		err.what = s;
		return err;
	}
}